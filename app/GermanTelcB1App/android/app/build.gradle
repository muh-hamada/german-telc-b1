apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
// Add the Google services Gradle plugin
apply plugin: "com.google.gms.google-services"
apply plugin: 'com.google.firebase.crashlytics'

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    /* Folders */
    //   The root of your project, i.e. where "package.json" lives. Default is '../..'
    // root = file("../../")
    //   The folder where the react-native NPM package is. Default is ../../node_modules/react-native
    // reactNativeDir = file("../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../../node_modules/@react-native/codegen
    // codegenDir = file("../../node_modules/@react-native/codegen")
    //   The cli.js file which is the React Native CLI entrypoint. Default is ../../node_modules/react-native/cli.js
    // cliFile = file("../../node_modules/react-native/cli.js")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    debuggableVariants = ["germanA1Debug", "germanA2Debug", "germanB1Debug", "germanB2Debug", "englishB1Debug", "englishB2Debug", "deleSpanishB1Debug"]

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]

    /* Autolinking */
    autolinkLibrariesWithApp()
}

def keystorePropertiesFile = rootProject.file("gradle.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = io.github.react-native-community:jsc-android-intl:2026004.+`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.mhamada.telcb1german"
    
    defaultConfig {
        applicationId "com.mhamada.telca2german"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 84
        versionName "1.0.84"
        missingDimensionStrategy "store", "play"
        
        // Explicitly specify ABI filters to ensure all architectures are included
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    
    // Configure packaging options to handle native libraries correctly
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            // Keep debug symbols for troubleshooting
            keepDebugSymbols += ["**/*.so"]
        }
        
        // Ensure React Native native libraries are not stripped
        doNotStrip '**/libreactnative.so'
        doNotStrip '**/libhermes.so'
        doNotStrip '**/libjsc.so'
        doNotStrip '**/libfbjni.so'
        doNotStrip '**/libc++_shared.so'
        
        // Explicitly exclude duplicates to prevent conflicts
        // Pick first occurrence if there are duplicate libraries
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        
        // Explicitly handle all React Native libraries
        pickFirst 'lib/armeabi-v7a/libreactnative.so'
        pickFirst 'lib/arm64-v8a/libreactnative.so'
        pickFirst 'lib/x86/libreactnative.so'
        pickFirst 'lib/x86_64/libreactnative.so'
        
        pickFirst 'lib/armeabi-v7a/libhermes.so'
        pickFirst 'lib/arm64-v8a/libhermes.so'
        pickFirst 'lib/x86/libhermes.so'
        pickFirst 'lib/x86_64/libhermes.so'
        
        pickFirst 'lib/armeabi-v7a/libfbjni.so'
        pickFirst 'lib/arm64-v8a/libfbjni.so'
        pickFirst 'lib/x86/libfbjni.so'
        pickFirst 'lib/x86_64/libfbjni.so'
        
        // Ensure META-INF files don't cause conflicts
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/*.kotlin_module'
    }
    
    flavorDimensions "exam"
    productFlavors {
        germanA1 {
            dimension "exam"
            applicationId "com.mhamada.telca1german"
            resValue "string", "app_name", "German TELC A1"
        }
        germanA2 {
            dimension "exam"
            applicationId "com.mhamada.telca2german"
            resValue "string", "app_name", "German TELC A2"
        }
        germanB1 {
            dimension "exam"
            applicationId "com.mhamada.telcb1german"
            resValue "string", "app_name", "German TELC B1"
        }
        germanB2 {
            dimension "exam"
            applicationId "com.mhamada.telcb2german"
            resValue "string", "app_name", "German TELC B2"
        }
        englishB1 {
            dimension "exam"
            applicationId "com.mhamada.telcb1english"
            resValue "string", "app_name", "English TELC B1"
        }
        englishB2 {
            dimension "exam"
            applicationId "com.mhamada.telcb2english"
            resValue "string", "app_name", "English TELC B2"
        }
        deleSpanishB1 {
            dimension "exam"
            applicationId "com.mhamada.deleb1spanish"
            resValue "string", "app_name", "Spanish DELE B1"
        }
    }
    
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['MYAPP_UPLOAD_STORE_FILE'])
                storePassword keystoreProperties['MYAPP_UPLOAD_STORE_PASSWORD']
                keyAlias keystoreProperties['MYAPP_UPLOAD_KEY_ALIAS']
                keyPassword keystoreProperties['MYAPP_UPLOAD_KEY_PASSWORD']
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            // Disable Firebase Analytics in debug builds
            manifestPlaceholders = [enableCrashReporting:false]
            resValue "string", "google_analytics_disabled", "true"
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            manifestPlaceholders = [enableCrashReporting:true]
            resValue "string", "google_analytics_disabled", "false"
        }
    }
    
    // Configure APK splits for different architectures
    // DISABLED: APK splits cause architecture mismatch issues with React Native
    // Use Android App Bundle (AAB) for Play Store instead, or universal APK for testing
    splits {
        abi {
            // Disable APK splitting by ABI to prevent architecture mismatch issues
            // that cause SoLoader crashes on Android 11/12
            enable false
            // Clear the default list
            reset()
            // Generate a universal APK that includes all ABIs
            universalApk true
        }
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    // Import the Firebase BoM
    implementation(platform("com.google.firebase:firebase-bom:34.4.0"))

    // Firebase dependencies
    implementation("com.google.firebase:firebase-analytics")
    implementation("com.google.firebase:firebase-auth")
    implementation("com.google.firebase:firebase-firestore")

    // Play Billing Library - required by Google Play (minimum 6.0.1)
    // This overrides the older AIDL version from react-native-google-mobile-ads
    implementation("com.android.billingclient:billing:7.1.1")

    // GIF support for animated images
    // Version should match React Native's bundled Fresco version
    implementation("com.facebook.fresco:animated-gif:3.4.0")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

// Copy react-native-vector-icons fonts to build folder
apply from: file("../../node_modules/react-native-vector-icons/fonts.gradle")
